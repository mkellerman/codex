set positional-arguments

# Display help
help:
    just -l

# `codex`
codex *args:
    cargo run --bin codex -- "$@"

# `codex exec`
exec *args:
    cargo run --bin codex -- exec "$@"

# `codex tui`
tui *args:
    cargo run --bin codex -- tui "$@"

# Run the CLI version of the file-search crate.
file-search *args:
    cargo run --bin codex-file-search -- "$@"

# format code
fmt:
    cargo fmt -- --config imports_granularity=Item

fix *args:
    cargo clippy --fix --all-features --tests --allow-dirty "$@"

install:
    rustup show active-toolchain
    cargo fetch

# Run `cargo nextest` since it's faster than `cargo test`, though including
# --no-fail-fast is important to ensure all tests are run.
#
# Run `cargo install cargo-nextest` if you don't have it installed.
test:
    cargo nextest run --no-fail-fast

# Run the MCP server
mcp-server-run *args:
    cargo run -p codex-mcp-server -- "$@"

# Build codex-cli and symlink it as `codex-dev` into ~/.cargo/bin
codex-dev-link:
    cargo build -p codex-cli -q
    mkdir -p "$HOME/.cargo/bin"
    ln -sf "$(pwd)/target/debug/codex" "$HOME/.cargo/bin/codex-dev"
    echo "Linked: $HOME/.cargo/bin/codex-dev -> $(pwd)/target/debug/codex"

# Remove the codex-dev symlink
codex-dev-unlink:
    rm -f "$HOME/.cargo/bin/codex-dev"
    echo "Removed: $HOME/.cargo/bin/codex-dev"
